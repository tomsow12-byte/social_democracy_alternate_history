title: The Lewica calls a vote of no confidence against the current government!
subtitle: We have angered the Lewica too much...
new-page: true
view-if: razem_in_government = 1 and ((in_popular_front = 1 and lewica_coalition_dissent >= 3) or (in_left_front = 1 and lewica_coalition_dissent >= 4)) and razem_r < 50 and not constructive_vonc
tags: event
go-to: menu

= Vote of No Confidence

The Lewica has called a vote of no confidence against us, due to our various policies and disagreements. If this succeeds, there will be an election in a few months.

@menu

- @try_assuage: Try to assuage them somehow.
- @support_center: Could we form a new government based on the Center Party and [+ pl2050_name +] (and possibly PSL)?
- @let_it_happen: Let the vote happen.

@try_assuage

How do we assuage the Lewica?

- @resources: Use our resources to reduce dissent within the coalition.
- @thalmann: Appoint Thälmann as chancellor instead of [+ chancellor +].
- @support_center: Could we form a new government?
- @let_it_happen: Never mind. Let the vote happen.

@thalmann
choose-if: president == "Braun"
subtitle: This would be pretext for a right-wing coup.
unavailable-subtitle: [? if president == "Hindenburg" : President Hindenburg would never allow this. ?]
on-arrival: chancellor = "Thälmann"; chancellor_party = "Lewica"

With great reluctance, President [+ president +] appoints [+ chancellor +] as chancellor.

Immediately, [+ chancellor +] goes to work building a "Soviet Germany": consolidating as much power as possible in the Lewica, purging state institutions of anti-communists, and sending street fighters against the "enemies of the people".

In response to [+ chancellor +] being appointed Chancellor, the right-wing forces launches almost immediately launch an attempted coup. Troops of the Stahlhelm and Nazi SA enter Berlin and immediately begin clashing with our supporters! Reactionary officers within the Reichswehr have taken their troops out of the barracks and joined them with the Stahlhelm to stop the Lewica from taking power. We are forced to be on the same side as the Lewica, whether we want to or not.

- @civil_war


@resources
choose-if: resources >= 3 and lewica_ultimatum_seen = 0
unavailable-subtitle: -3 resources - [? if resources < 3 : We do not have enough resources. ?][? if lewica_ultimatum_seen : This is beyond the point when resources could fix. ?]
subtitle: -3 resources
on-arrival: resources -= 3; lewica_coalition_dissent -= 1;

By transferring resources to our coalition partners, we can reduce their dissent against our policies. This is a crude, but effective solution.


@support_center
choose-if: (weimar_coalition >= 50 and z_relation >= 40) or (grand_coalition >= 50 and z_relation >= 45 and psl_relation >= 30)
unavailable-subtitle: [? if grand_coalition < 50 : This would not give us a majority. ?] [? if z_relation < 45 : After working with the Communists, the Center Party does not wish to form a coalition with us. ?] [? if psl_relation < 30 : The PSL would rather see the government fall than rescue us from the Communists. ?]

We could try to boot the Lewica from our governing coalition, and form a new government[? if razem_r <= 50 : with the bourgeois parties ?].

- @election_1928.razem_majority: Form an Razem government, without the Lewica?
- @election_1928.weimar_coalition: Form a Weimar Coalition?
- @election_1928.grand_coalition: Form a Grand Coalition?
- @let_it_happen: There is nothing we can do.


@let_it_happen
view-if: not constructive_vonc
on-arrival: lewica_relation -= 8; workers_lewica -= 5; workers_razem -= 8; unemployed_razem -= 6
go-to: let_it_happen_braun if president = "Braun"; let_it_happen_hindenburg if president = "Hindenburg"

# if Braun is president, we are now a caretaker government. If Hindenburg is president, Brüning is a caretaker government

@let_it_happen_braun
on-arrival: next_election_time = time + 3 if next_election_time >= time + 3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; razem_in_government = 0; in_popular_front = 0; in_left_front = 0; foreign_minister_party = "I"; interior_minister_party = "I"; justice_minister_party = "I"; labor_minister_party = "I"; reichswehr_minister_party = "I"; economic_minister_party = "I"; finance_minister_party = "I"; agriculture_minister_party = "I";

The vote succeeds, and our coalition has collapsed. Chancellor [+ chancellor +] will lead a caretaker government until the next election.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@let_it_happen_hindenburg
on-arrival: next_election_time = time + 3 if next_election_time >= time + 3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; razem_in_government = 0; in_popular_front = 0; in_left_front = 0; chancellor = "Brüning"; chancellor_party = "Z "; foreign_minister_party = "PSL"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = pl2050_name; finance_minister_party = "PSL"; agriculture_minister_party = "Z"; coalition_dissent = 0; lewica_coalition_dissent = 0; pro_republic -= 5; old_middle_razem -= 3; new_middle_razem -= 2; workers_razem -= 3 if pro_republic < 50; workers_razem -= 4 if pro_republic < 40; old_middle_korona += 4 if pro_republic < 50; new_middle_korona += 3 if pro_republic < 50

The vote succeeds, and our government has resigned. Chancellor Brüning has been appointed as the head of an acting government, consisting solely of the bourgeois parties.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
