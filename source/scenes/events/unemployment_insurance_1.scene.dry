title: Unemployment Insurance Crisis
subtitle: The right wing of our coalition presents an ultimatum.
new-page: true
view-if: razem_in_government = 1 and (year = 1930 or year = 1931 or year = 1932) and (month >= 3) and in_grand_coalition = 1 and unemployment_insurance_timer = 0 and (unemployed >= unemployment_insurance_threshold + 4 or (unemployed >= 20 and unemployed >= unemployment_insurance_threshold)) and not constructive_vonc and president == "Hindenburg"
on-arrival: unemployment_insurance_crisis += 1; budget -= 1 if budget >= 1; unemployment_insurance_timer = 10; crisis_urgency += 1; unemployment_insurance_threshold = unemployed
tags: event
max-visits: 2
go-to: what_do if unemployment_insurance_crisis = 2
face-image: img/Kollwitz_Arbeitslosigkeit.jpg

= Unemployment Insurance Crisis

The economic crisis has caused mass layoffs all throughout Germany, affecting the industrial working class the hardest. Our welfare spending is increasing due to the greater need.

In the midst of all this, our right-wing coalition partners in the PSL and Center parties have a plan to cut benefits for the unemployed, lower taxes on the rich, and raise taxes on the working class, as part of a plan to balance the budget and discipline labor. They do not see a possibility for compromise. If we do not accept, they could blow up the coalition.

[? if unemployment_insurance_crisis = 2 : This is the second such ultimatum thrown by the Right. Obviously, the bourgeoisie are trying to remove us from the government. ?]

- @tax_rich: Why can't we simply tax the rich?
- @gift: This is a gift to the Communists.

@tax_rich
max-visits: 1
go-to: what_do

The bourgeois parties, as representatives of the rich industrialists, would not have that.

@gift
go-to: what_do

This is a gift to the Communists, a boost to their theory that parliamentary democracy will never benefit the working class.

@what_do
on-arrival: psl_relation += 5 if chancellor = "Braun"

But the crux of the matter is, what can we do?

- @destroy: Fine. We will end this coalition.
- @accept: Unfortunately, we must accept the Right's demands for the sake of stability.
- @compromise: Surely there is some compromise to be had?

@destroy
call: set_next_election_time
on-arrival: time_to_election = 3; razem_in_government = 0; in_grand_coalition = 0; razem_toleration = 0; z_relation -= 8; psl_relation -= 10; chancellor = "Br端ning"; chancellor_party = "Z "; welfare -= 2; budget += 1; inflation -= 2; foreign_minister_party = "PSL"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = pl2050_name; finance_minister_party = "PSL"; agriculture_minister_party = "Z"; razem_no_confidence = 1; pro_republic -= 4; old_middle_razem -= 3; new_middle_razem -= 2; rural_razem -= 2; catholics_razem -= 2; economic_growth -= 0.6

# TODO: if Braun is president, don't do this...

Fine. If the bourgeoisie wish to blow up the government, let them do so. Socialists will not be complicit in the destruction of the working class.

[? if chancellor_party = "Razem" : Heinrich Br端ning of the Center Party has been appointed Chancellor. ?] Br端ning now leads an all-bourgeois cabinet. Using the emergency powers of Article 48, Br端ning and President Hindenburg have passed the welfare cuts over the objections of the Reichstag.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@accept
on-arrival: workers_razem -= 0.15*workers_razem; unemployed_razem -= 0.3*unemployed_razem; workers_lewica += 7; workers_korona += 7; unemployed_lewica += 5; unemployed_korona += 5; left_dissent += 15; center_dissent += 10; labor_dissent += 15; reformist_dissent += 5; welfare -= 2; budget += 1; unemployed += 3; month += 1; pro_republic -= 4; lewica_relation -= 8; inflation -= 2; economic_growth -= 0.6

Unfortunately, we must accept this ultimatum if we are to maintain this coalition. The Nazis and Communists are waiting in the wings for us to fail, ready to pounce upon the instability that would result from a breakup of the governing coalition.

The workers are furious at our betrayal, as are a good portion of our membership throughout the factions. Our loss is the Lewica's gain, and even the NSDAP, those lapdogs of the bourgeoisie, have joined in criticizing us for our austerity.


@compromise
unavailable-subtitle: We do not have good enough relations with the right-wing parties.
choose-if: psl_relation >= 35

Thanks to our good relations, the PSL have offered a compromise that raises some taxes on the wealthy, along with lesser welfare cuts. Our members have mixed opinions on accepting this compromise. Reformists and Centrists support maintaining our government to keep the fascists and communists out, while Labor generally opposes any cuts, and the Left is opposed to compromises with the bourgeoisie.

- @accept_compromise: Accept the compromise.
- @destroy: Reject the compromise and let this government fall.

@accept_compromise
on-arrival: workers_razem -= 5; unemployed_razem -= 6; workers_lewica += 5; unemployed_lewica += 3; unemployed_korona += 3; left_dissent += 10; labor_dissent += 8; center_dissent += 5; welfare -= 1; budget += 1; month += 1; lewica_relation -= 4; inflation -= 1; economic_growth -= 0.3

We have accepted the compromise offered by the bourgeoisie. Many of the workers and unemployed see this as a betrayal, although we present it as the successful aversion of a much worse alternative.
